apply plugin: 'groovy'
apply plugin: 'antlr'

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.12'
  compile group: 'com.github.julianthome', name: 'inmemantlr-api', version: '1.3.8'

  compile group: 'org.ghost4j', name: 'ghost4j', version: '1.0.1'

  antlr "org.antlr:antlr4:4.7"
  testCompile 'junit:junit:4.12'
}

sourceSets {
  main {
    groovy {
      srcDirs = ['src/main/groovy', 'src/main/java']
    }

    java {
      srcDirs = []
    }
  }
}

generateGrammarSource {
  maxHeapSize = "64m"
  arguments += [ "-visitor", "-long-messages" ]
}

task antlrJS(type: AntlrTask) {
  maxHeapSize = "64m"
  outputDirectory = new File("${project.buildDir}/generated-src/antlr/${sourceSets.main.name}/js")
  arguments += [ "-Dlanguage=JavaScript"]
}

task testRig { }

def dir = 'src/test/rig'
FileTree tree = fileTree( dir: dir, include: '**/*.adoc' )

tree.each { File file ->
  task "rig_${file.name}" (type: com.vperi.util.TestRigTask, dependsOn: 'build') {
    classpath = sourceSets.main.runtimeClasspath
    grammar = "Asciidoc"
    startRule = "asciidoc"
    src = file
  }

  testRig.dependsOn "rig_${file.name}"
}
